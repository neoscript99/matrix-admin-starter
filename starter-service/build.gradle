buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
    }
}

plugins {
	id 'org.springframework.boot' version '2.2.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'groovy'
}

group = 'com.feathermind.matrix.starter'
version = 1.0
sourceCompatibility = '1.8'

repositories {
    //mavenLocal()
    maven {
        credentials {
            username 'BKfdYT'
            password System.getenv('ALIYUN_PASSWORD')
        }
        url 'https://packages.aliyun.com/maven/repository/2092094-release-IiCP3V/'
    }
    maven {
        credentials {
            username 'BKfdYT'
            password System.getenv('ALIYUN_PASSWORD')
        }
        url 'https://packages.aliyun.com/maven/repository/2092094-snapshot-5LY1FN/'
    }
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    implementation ('com.feathermind.matrix:matrix-admin:1.6.10')
    implementation 'org.projectlombok:lombok:1.18.16'
    //gradle支持lombok编译
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    testImplementation("org.spockframework:spock-spring:1.3-groovy-2.5")
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    systemProperty 'spring.profiles.active', 'dev'
    systemProperty 'init', 'true'
    systemProperties System.properties
}

task copyReact(type: Copy) {
    from('../starter-ui/build/') {
        include '**/*.*'
    }
    into 'build/resources/main/static/'
}

evaluationDependsOn(":starter-ui")
def uiBuild = project(":starter-ui").tasks.uiBuild
bootJar {
    copyReact.mustRunAfter(uiBuild)
    dependsOn(uiBuild, copyReact)
}
